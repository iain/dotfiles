" This vimrc automatically installs everything it needs.
" To install, or reinstall, remove ~/.vim directory and open Vim.

if &compatible
  set nocompatible
endif

filetype off

let needsToInstallBundles=0
if !filereadable(expand("~/.vim/autoload/plug.vim"))
  echo "\nInstalling Vim dependencies... Please be patient!\n"
  silent !mkdir -p ~/.vim/tmp
  silent !mkdir -p ~/.vim/swap
  silent !mkdir -p ~/.vim/undo
  silent !mkfifo ~/.vim/commands-fifo
  " silent !git clone https://github.com/VundleVim/Vundle.vim.git ~/.vim/bundle/Vundle.vim
  silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
  let needsToInstallBundles=1
endif

call plug#begin('~/.vim/plugged')

" Do these first, because other plugins depend on them
Plug 'VundleVim/Vundle.vim'
" Plug 'Syntastic'
Plug 'tpope/vim-fugitive'
Plug 'w0rp/ale'

" Colorschemes
Plug 'twerth/ir_black'
Plug 'nanotech/jellybeans.vim'

" Syntax & Language Support
Plug 'sheerun/vim-polyglot'
Plug 'tpope/vim-rails'

" Miscellany
Plug 'csexton/trailertrash.vim'
Plug 'adelarsq/vim-matchit'
Plug 'godlygeek/tabular'
Plug 'kien/ctrlp.vim'
Plug 'mileszs/ack.vim'
Plug 'tomtom/tcomment_vim'
Plug 'tpope/vim-abolish'
Plug 'tpope/vim-sensible'
Plug 'tpope/vim-unimpaired'
Plug 'tpope/vim-vinegar'
Plug 'michaeljsmith/vim-indent-object'
Plug 'janko-m/vim-test'
Plug 'luochen1990/rainbow'
Plug 'jlanzarotta/bufexplorer'
Plug 'itchyny/lightline.vim'
Plug 'tpope/vim-rhubarb' " Gbrowse

" Under consideration
Plug 'tpope/vim-surround'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-eunuch'
Plug 'mbbill/undotree'
Plug 'vim-scripts/Enter-Indent'
Plug 'rking/vim-detailed'

call plug#end()

if needsToInstallBundles == 1
  echo "\nInstalling plugins.\n"
  :PlugInstall
  echo "\nInstalled.\n"
endif


" Trying to get rid of SEGVs
" https://github.com/junegunn/vim-plug/wiki/faq#vim-caught-deadly-signal-segv
" let g:plug_threads = 1

filetype plugin indent on

" ==========================
" SETTINGS
" ==========================

" theme settings
let g:jellybeans_use_gui_italics = 0
" let g:jellybeans_overrides = { 'background': { 'guibg': '000000' } }
let g:jellybeans_overrides = { 'SignColumn': { 'guibg': '232323' } }
let g:jellybeans_background_color = '232323'

colorscheme jellybeans

set antialias                 " Pretty
set guioptions=Ace            " No menubar, toolbar or scrollbars, as minimal as possible
set guifont=Hack\ Regular:h13 " Get Hack here: http://sourcefoundry.org/hack/ or https://github.com/powerline/fonts
set fuoptions=maxvert,maxhorz " Fixes Full Screen on OSX
" If you are on OSX Lion, and you hate Lion's native full screen, turn it off for macvim:
"   defaults write org.vim.MacVim MMNativeFullScreen 0
" Press Ctrl+Cmd+F to go full screen

" Turn off balloons
set noballooneval
set balloondelay=100000
setlocal balloonexpr=

set t_Co=256               " 256-bit color
set vb t_vb=               " Turn off beep
set lazyredraw             " Don't redraw during macro execution
set synmaxcol=512          " Stop syntax highlighting for long lines
" set relativenumber         " Relative line numbering
set number                 " Relative line numbering
set nowrap                 " No wrapping by default
set scrolloff=4            " Keep a few lines above and below current line
set equalalways            " create equally sized splits
set splitbelow splitright  " split placement
" set wildmode=longest,list  " Makes completion in command mode like bash
set history=10000          " Keep a lot of stuff in history
set backup                 " Make backups
set backupdir=~/.vim/tmp/  " Keep backups in a central location
set directory=~/.vim/swap/ " Keep swap files in a central location
set undofile               " Keep undo history even after closing Vim
set undodir=~/.vim/undo    " Where to store undo history
set timeoutlen=500         " Don't wait so long for ambiguous leader keys
set noesckeys              " Get rid of the delay when hitting esc!
set gdefault               " assume the /g flag on :s substitutions to replace all matches in a line

" Indenting always 2 spaces, sorry Python
set smartindent
set cindent
set expandtab
set shiftwidth=2
set softtabstop=2
set tabstop=2

" Search
set smartcase
set hlsearch

" also use jsx syntax in .js files
let g:jsx_ext_required = 0

let g:ruby_no_expensive = 0 " Differentiate between do..end and class..end is slow
let g:ruby_operators = 1    " Highlight Ruby operators
let g:ruby_indent_block_style = 'do'

" let g:netrw_nobeval = 1

" The Silver Searcher, source: https://robots.thoughtbot.com/faster-grepping-in-vim
if executable('ag')
  " Use ag over grep
  set grepprg=ag\ --nogroup\ --nocolor
  " Use ag in CtrlP for listing files. Lightning fast and respects .gitignore
  let g:ctrlp_user_command = 'ag %s -l --nocolor -g ""'
  " ag is fast enough that CtrlP doesn't need to cache
  let g:ctrlp_use_caching = 0
  let g:ackprg = 'ag --nogroup --nocolor --column'
  " Use K to search for word at current cursor
  " nnoremap <silent> K :grep! "\b<C-R><C-W>\b"<CR>:cw<CR>
  " bind \ (backward slash) to grep shortcut
  command! -nargs=+ -complete=file -bar Ag silent! grep! <args>|cwindow|redraw!
  nnoremap \ :Ag<SPACE>
endif

set formatprg=par\ -w80\ -q

" When editing a file, always jump to the last known cursor position.
autocmd BufReadPost *
      \ if line("'\"") > 1 && line("'\"") <= line("$") |
      \   exe "normal! g`\"" |
      \ endif

" Format xml files
" au FileType xml exe ":silent 1,$!xmllint --format --recover - 2>/dev/null"

" ==========================
" GENERAL KEY MAPPINGS
" ==========================

" Rename :W to :w
command! W :w

" Use OSX pbpaste/pbcopy for F1/F2, for use in terminal
nmap <F1> :set paste<cr>:r !pbpaste<cr>:set nopaste<cr>
imap <F1> <Esc>:set paste<cr>:r !pbpaste<cr>:set nopaste<cr>
nmap <F2> :.w !pbcopy<cr><cr>
vmap <F2> :w !pbcopy<cr><cr>

" in insert mode, jj goes to normal mode
" if you ever need to type jj for real, type it slowly, like on old school mobile phones
inoremap jj <ESC>
inoremap jk <ESC>
map <C-C> <ESC>

" Directly switch between open splitted windows
map <C-J> <C-W>j
map <C-H> <C-W>h
map <C-L> <C-W>l
map <C-K> <C-W>k

" pressing j or k in a long wrapped will put cursor down/up one visual line
nnoremap j gj
nnoremap k gk

" Shift+K becomes similar to Shift+J
nnoremap <S-k> kJ

" Remap return to clear search highlight
nnoremap <cr> :nohlsearch<cr>

" highlight the 100th character
" highlight ColorColumn ctermbg=magenta
" call matchadd('ColorColumn', '\%101v', 100)

" In normal mode, space will start command mode.
nnoremap <space> :e 
vnoremap <space> :e 

" Buffer Explorer opens with Ctrl+B
nnoremap <C-B> :BufExplorer<cr>

" %% will become the directory of the current file
cnoremap %% <C-R>=expand('%:h').'/'<cr>

" Hooks up smart tab autocomplete behavior mentioned above
inoremap <tab> <c-r>=InsertTabWrapper()<cr>
inoremap <s-tab> <c-n>

" A common typo is ":E" when actually meaning ":e" and not ":Explore" or ":Errors".
cnoreabbrev E e

" Insert hex keys
inoremap <silent> <F12> <ESC>:r ! ruby -r securerandom -e "print SecureRandom.hex(32)"<CR>kJ

" ==========================
" LEADER KEYS
" ==========================

let mapleader = ","

" Map ,e and ,v to open files in the same directory as the current file
map <leader>e :edit %%
map <leader>v :view %%
map <leader>m :vsplit %%
map <leader>n :split %%


" Custom functions
map <leader>p :call PromoteToLet()<cr>

" Align = signs
nmap <Leader>a= :Tabularize /=<cr>
vmap <Leader>a= :Tabularize /=<cr>gv
" Align hashrockets
nmap <Leader>a> :Tabularize /=><cr>
vmap <Leader>a> :Tabularize /=><cr>gv
" Align commas
nmap <Leader>a, :Tabularize /,\zs/l1<cr>
vmap <Leader>a, :Tabularize /,\zs/l1<cr>gv
" Align words and fix indentation
nmap <Leader>aw :Tabularize /\s\+\zs/l1<cr>=ip
vmap <Leader>aw :Tabularize /\s\+\zs/l1<cr>gv=gv
" Align comments
nmap <Leader>ac :Tabularize /#<cr>
" Align blocks
nmap <Leader>a{ :Tabularize /{<cr>
vmap <Leader>a{ :Tabularize /{<cr>gv
vmap <Leader>a{ :Tabularize /{<cr>gv

" Fugitive (Git)
map <Leader><Leader>c :Gcommit<cr>
map <Leader><Leader>b :Gblame<cr>
map <Leader><Leader>s :Gstatus<cr>
map <Leader><Leader>w :Gwrite<cr>
map <Leader><Leader>p :Git push<cr>
map <Leader><Leader>f :Git fetch<cr>
map <Leader><Leader>u :Git up<cr>

" Open notes file
map <Leader>q :split ~/Dropbox/notes.md<cr>



" ==========================
" FUNCTIONS
" ==========================

" Smart tab autocomplete behavior
function! InsertTabWrapper()
  let col = col('.') - 1
  if !col || getline('.')[col - 1] !~ '\k'
    return "\<tab>"
  else
    return "\<c-p>"
  endif
endfunction

" Convert local variable to RSpec let block
function! PromoteToLet()
  :normal! dd
  :exec '?^\s*describe\|fdescribe\|fcontext\|context\|RSpec\.describe\>'
  :normal! p
  :.s/\(\w\+\)\s\+=\s\+\(.*\)$/let(:\1) { \2 }/
  :normal ==
endfunction

" Aligns tables in cucumber, from tim pope: https://gist.github.com/tpope/287147
function! s:AlignCucumberTables()
  let p = '^\s*|\s.*\s|\s*$'
  if exists(':Tabularize') && getline('.') =~# '^\s*|' && (getline(line('.')-1) =~# p || getline(line('.')+1) =~# p)
    let column = strlen(substitute(getline('.')[0:col('.')],'[^|]','','g'))
    let position = strlen(matchstr(getline('.')[0:col('.')],'.*|\s*\zs.*'))
    Tabularize/|/l1
    normal! 0
    call search(repeat('[^|]*|',column).'\s\{-\}'.repeat('.',position),'ce',line('.'))
  endif
endfunction

" Only enable in cucumber
function! EnableCucumberTables()
  inoremap <silent> <Bar>   <Bar><Esc>:call <SID>AlignCucumberTables()<cr>a
endfunction
au FileType cucumber call EnableCucumberTables()
au FileType markdown call EnableCucumberTables()

function! AlignRubyHashes()
  let p = '^\s*\w\+:\s.*,$'

  " Calculate the range to act. Default Tabularize doesn't do this properly,
  " because the range is determined by a different regex than the alignment
  " regex.
  let from = line('.')
  while (getline(from - 1) =~ p)
    let from -= 1
  endwhile

  let to = line('.')
  while (getline(to + 1) =~ p)
    let to += 1
  endwhile

  " Call Tabularize with the new range
  exe from.','.to.'Tabularize/\(:.*\)\@<!\(:\s\)\zs/l1'
  normal! 0

endfunction

" Align after colons, but only the first one in the line,
" In normal mode, determine range automatically
nmap <Leader>a: :call AlignRubyHashes()<cr>
" In visual mode, the range is already fixed
vmap <Leader>a: :Tabularize/\(:.*\)\@<!:\zs/l1<cr>gv

" linters and syntax checkers:
let g:ale_sign_error   = ' •' " has U+2009 as space
let g:ale_sign_warning = ' •'
let g:ale_sign_column_always = 1
hi link ALEErrorSign    Error
hi link ALEWarningSign  Warning
hi Error   cterm=bold gui=bold guibg=#232323 guifg=DarkRed
hi Warning cterm=bold gui=bold

let g:ale_statusline_format = ['⨉ %d', 'w %d', 'OK']
let g:ale_ruby_rubocop_options = '-c ~/.dotfiles/rubocop/default.yml'
let g:ale_set_loclist = 0
let g:ale_set_quickfix = 0
" let g:ale_lint_on_text_changed = 'normal'
" Only run on save
" let g:ale_lint_on_text_changed = 'never'
" if you don't want linters to run on opening a file
" let g:ale_lint_on_enter = 0

" Map prev and back mac shortcuts to traverse buffers
map <D-[> :bprevious<cr>
map <D-]> :bnext<cr>

nmap <silent> <D-k> <Plug>(ale_previous)
nmap <silent> <D-j> <Plug>(ale_next)

" vim-test configuration
let test#strategy = "iterm"
let g:test#preserve_screen = 1
let test#ruby#rspec#executable = 'rspec_focus'
let test#ruby#cucumber#executable = 'cucumber_focus'
let test#elixir#exunit#options = '--stale'
let test#elixir#exunit#options = {
  \ 'suite':   '--stale',
\}
nmap <silent> <leader>t :up<CR>:TestFile<CR>
nmap <silent> <leader>T :up<CR>:TestNearest<CR>
nmap <silent> <leader>l :up<CR>:TestSuite --only-failures<CR>
nmap <silent> <leader>r :up<CR>:TestSuite<CR>
nmap <silent> <leader>g :TestVisit<CR>

" status line (using lightline)
set laststatus=2
set noshowmode
let g:lightline = {
      \ 'colorscheme': 'jellybeans',
      \ 'mode_map': { 'c': 'NORMAL' },
      \ 'active': {
      \   'left': [ [ 'mode', 'paste' ], [ 'filename' ] ],
      \   'right': [ [ 'syntastic', 'lineinfo' ], ['percent'], [ 'fileformat', 'fileencoding', 'filetype' ] ]
      \ },
      \ 'component_function': {
      \   'modified': 'LightlineModified',
      \   'readonly': 'LightlineReadonly',
      \   'fugitive': 'LightlineFugitive',
      \   'filename': 'LightlineFilename',
      \   'fileformat': 'LightlineFileformat',
      \   'filetype': 'LightlineFiletype',
      \   'fileencoding': 'LightlineFileencoding',
      \   'mode': 'LightlineMode',
      \ },
      \ 'component_expand': {
      \   'syntastic': 'ALEGetStatusLine',
      \ },
      \ 'component_type': {
      \   'syntastic': 'error',
      \ },
      \ 'separator': { 'left': '', 'right': '' },
      \ 'subseparator': { 'left': '', 'right': '' }
      \ }

function! LightlineModified()
  return &ft =~ 'help\|vimfiler\|gundo' ? '' : &modified ? '+' : &modifiable ? '' : '-'
endfunction

function! LightlineReadonly()
  return &ft !~? 'help\|vimfiler\|gundo' && &readonly ? '' : ''
endfunction

" \ '' != expand('%:t') ? expand('%:t') : '[No Name]') .
function! LightlineFilename()
  return ('' != LightlineReadonly() ? LightlineReadonly() . ' ' : '') .
        \ (&ft == 'vimfiler' ? vimfiler#get_status_string() :
        \  &ft == 'unite' ? unite#get_status_string() :
        \  &ft == 'vimshell' ? vimshell#get_status_string() :
        \ '' != expand('%:t') ? @% : '[No Name]') .
        \ ('' != LightlineModified() ? ' ' . LightlineModified() : '')
endfunction

function! LightlineFugitive()
  if &ft !~? 'vimfiler\|gundo' && exists("*fugitive#head")
    let branch = fugitive#head()
    return branch !=# '' ? ' '.branch : ''
  endif
  return ''
endfunction

function! LightlineFileformat()
  return winwidth(0) > 70 ? &fileformat : ''
endfunction

function! LightlineFiletype()
  return winwidth(0) > 70 ? (&filetype !=# '' ? &filetype : 'no ft') : ''
endfunction

function! LightlineFileencoding()
  return winwidth(0) > 70 ? (&fenc !=# '' ? &fenc : &enc) : ''
endfunction

function! LightlineMode()
  return winwidth(0) > 60 ? lightline#mode() : ''
endfunction

" Add rainbows and unicorns
let g:rainbow_active = 1
let g:rainbow_conf = {
  \  'guifgs':  [ '#ea8986', '#a4c38c', '#ffc68d', '#a7cae3', '#e8cefb', '#00a7a0' ],
  \  'ctermfgs': ['lightmagenta', 'lightblue', 'lightyellow', 'lightcyan'],
  \  'operators': '_,_',
  \  'parentheses': ['start=/(/ end=/)/ fold', 'start=/\[/ end=/\]/ fold', 'start=/{/ end=/}/ fold'],
  \  'separately': {
  \    '*': {},
  \    'tex': { 'parentheses': ['start=/(/ end=/)/', 'start=/\[/ end=/\]/'] },
  \    'vim': { 'parentheses': ['start=/(/ end=/)/', 'start=/\[/ end=/\]/', 'start=/{/ end=/}/ fold', 'start=/(/ end=/)/ containedin=vimFuncBody', 'start=/\[/ end=/\]/ containedin=vimFuncBody', 'start=/{/ end=/}/ fold containedin=vimFuncBody'] },
  \    'html': { 'parentheses': ['start=/\v\<((area|base|br|col|embed|hr|img|input|keygen|link|menuitem|meta|param|source|track|wbr)[ >])@!\z([-_:a-zA-Z0-9]+)(\s+[-_:a-zA-Z0-9]+(\=("[^"]*"|'."'".'[^'."'".']*'."'".'|[^ '."'".'"><=`]*))?)*\>/ end=#</\z1># fold'], },
  \    'eruby': { 'parentheses': ['start=/\v\<((area|base|br|col|embed|hr|img|input|keygen|link|menuitem|meta|param|source|track|wbr)[ >])@!\z([-_:a-zA-Z0-9]+)(\s+[-_:a-zA-Z0-9]+(\=("[^"]*"|'."'".'[^'."'".']*'."'".'|[^ '."'".'"><=`]*))?)*\>/ end=#</\z1># fold'], },
  \    'css': 0,
  \  }
  \}
