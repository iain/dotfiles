"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Iain's Vim configuration: ~/.vimrc
"
" This vimrc automatically installs everything it needs.
" To install, or reinstall, remove ~/.vim directory and open Vim.
"
if &compatible
  set nocompatible
endif

filetype off

let needsToInstallPlugins=0
if !filereadable(expand("~/.vim/autoload/plug.vim"))
  echo "\nInstalling Vim dependencies... Please be patient!\n"
  silent !mkdir -p ~/.vim/tmp
  silent !mkdir -p ~/.vim/swap
  silent !mkdir -p ~/.vim/undo
  silent !mkfifo ~/.vim/commands-fifo
  silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
  let needsToInstallPlugins=1
endif

call plug#begin('~/.vim/plugged')

Plug 'w0rp/ale'                           " Async Linter Engine
Plug 'adelarsq/vim-matchit'               " % jumps between do/end etc
Plug 'csexton/trailertrash.vim'           " hate trailing whitespace
Plug 'godlygeek/tabular'                  " vertically align stuff
Plug 'itchyny/lightline.vim'              " fancy statusbar
Plug 'janko-m/vim-test'                   " quickly run tests in terminal
Plug 'jlanzarotta/bufexplorer'            " lists open bufffers like netrw
Plug 'kien/ctrlp.vim'                     " fuzzy search files
Plug 'luochen1990/rainbow'                " rainbow parenthesis
Plug 'michaeljsmith/vim-indent-object'    " defines textobjects based on indent-level
Plug 'sheerun/vim-polyglot'               " contains a ton of syntax plugins
Plug 'tomtom/tcomment_vim'                " quickly comment/uncomment code
Plug 'tpope/vim-fugitive'                 " git integration
Plug 'tpope/vim-rails'                    " rails integration
Plug 'tpope/vim-rhubarb'                  " open file in github.com
Plug 'tpope/vim-sensible'                 " sensible defaults
Plug 'tpope/vim-unimpaired'               " handy toggles
Plug 'tpope/vim-repeat'                   " improved repeat button .
Plug 'tpope/vim-surround'                 " quickly change quotes/parens

" Plug 'tpope/vim-vinegar'                  " handy shortcuts for netrw
Plug 'justinmk/vim-dirvish' " netrw replacer

" Under evaluation
Plug 'mbbill/undotree'                    " shows your undotree
Plug 'tpope/vim-eunuch'                   " unix commands like :Move
" Plug 'rking/vim-detailed'                 " supposedly more detailed syntax highlighting
" Plug 'tpope/vim-abolish'                  " abbreviations and case modifier
" Plug 'vim-scripts/Enter-Indent'           " fixes indentation off open/close tags
" Plug 'airblade/vim-gitgutter'             " git change signs in the gutter

Plug 'nanotech/jellybeans.vim' " colorscheme
" Plug 'twerth/ir_black'       " old colorscheme
" Plug 'fenetikm/falcon'
" Plug 'dracula/vim', { 'as': 'dracula' }

Plug 'jremmen/vim-ripgrep'

call plug#end()

if needsToInstallPlugins == 1
  echo "\nInstalling plugins.\n"
  :PlugInstall
  echo "\nInstalled.\n"
endif

let g:dirvish_mode = ':sort ,^.*[\/],'
" autocmd FileType dirvish silent keeppatterns g@\v/\.[^\/]+/?$@d _
autocmd FileType dirvish silent set nocursorline

augroup my_dirvish_events
  autocmd!

  autocmd FileType dirvish map <buffer> % :edit <C-R>=expand('%:h').'/'<cr>
  autocmd FileType dirvish map <buffer> d :!mkdir -p <C-R>=expand('%:h').'/'<cr>
  autocmd FileType dirvish map <buffer> D :!rm -r <C-R>=expand('<cfile>')<cr>
  autocmd FileType dirvish map <buffer> r :!mv <C-R>=expand('<cfile>')<cr> <C-R>=expand('<cfile>')<cr>

  " autocmd FileType dirvish silent keeppatterns g@\v/\.[^\/]+/?$@d _

augroup END

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Vim and Macvim (not neovim)
"
filetype plugin indent on

" for neovim, change these directories
set backupdir=~/.vim/tmp/  " Keep backups in a central location
set directory=~/.vim/swap/ " Keep swap files in a central location
set undodir=~/.vim/undo    " Where to store undo history

set t_Co=256  " 256-bit color
set vb t_vb=  " Turn off beeping

" Remove tooltips
set noballooneval
set balloondelay=100000
setlocal balloonexpr=

" set antialias                 " Turn on antialias
set guioptions=Ace            " No menubar, toolbar or scrollbars, as minimal as possible
set guifont=Hack\ Regular:h15 " Get Hack here: http://sourcefoundry.org/hack/ or https://github.com/powerline/fonts
set fuoptions=maxvert,maxhorz " Fixes Full Screen on OSX
" If you are on OSX Lion, and you hate Lion's native full screen, turn it off for macvim:
"   defaults write org.vim.MacVim MMNativeFullScreen 0
" Press Ctrl+Cmd+F to go full screen


au BufNewFile,BufRead *.template set filetype=yaml

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Jellybean Colorscheme
"
let g:jellybeans_use_gui_italics = 0
let g:jellybeans_overrides = { 'SignColumn': { 'guibg': '151515' } }
let g:jellybeans_background_color = '151515'
colorscheme jellybeans
set fillchars+=vert:\  " space as vertical split character

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Formatting configuration
"
set smartcase
set hlsearch

" Indenting always 2 spaces, sorry Python
set smartindent
set cindent
set expandtab
set shiftwidth=2
set softtabstop=2
set tabstop=2

if executable('par')
  set formatprg=par\ -w80\ -q
endif


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" General key remapping
"
let mapleader = ","

" Allow :W to save
command! W :w

" A common typo is ":E" when actually meaning ":e" and not ":Explore" or ":Errors".
cnoreabbrev E e

" in insert mode, jj or jk goes to normal mode
" if you ever need to type jj for real, type it slowly, like on old school mobile phones
inoremap jj <ESC>
inoremap jk <ESC>
map <C-C> <ESC>

" Shift+K becomes similar to Shift+J
nnoremap <S-k> kJ

" Remap return to clear search highlight
nnoremap <cr> :nohlsearch<cr>
" Restore return to default for quickfix window
autocmd BufReadPost quickfix nnoremap <buffer> <CR> <CR>

" Open notes file
map <Leader>q :split ~/Dropbox/notes.md<cr>


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Window configuration
"
set number                 " Line numbering
set nowrap                 " No wrapping by default
set scrolloff=4            " Keep a few lines above and below current line
set equalalways            " create equally sized splits
set splitbelow splitright  " split placement
" set relativenumber       " Relative line numbering

" Directly switch between open splitted windows
map <C-J> <C-W>j
map <C-H> <C-W>h
map <C-L> <C-W>l
map <C-K> <C-W>k

" %% will become the directory of the current file
cnoremap %% <C-R>=expand('%:h').'/'<cr>

" Map ,e and ,v to open files in the same directory as the current file
map <leader>e :edit %%
map <leader>v :view %%
map <leader>m :vsplit %%
map <leader>n :split %%


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Syntax configuration
"
set lazyredraw     " Don't redraw during macro execution
set synmaxcol=512  " Stop syntax highlighting for long lines

let g:jsx_ext_required = 0  " also use jsx syntax in .js files
let g:ruby_no_expensive = 0 " Differentiate between do..end and class..end is slow
let g:ruby_operators = 1    " Highlight Ruby operators
let g:ruby_indent_block_style = 'do'


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Buffer configuration
"
set history=10000           " Keep a lot of stuff in history
set backup                  " Make backups
set undofile                " Keep undo history even after closing Vim
set timeoutlen=500          " Don't wait so long for ambiguous leader keys
set gdefault                " assume the /g flag on :s substitutions to replace all matches in a line

" Buffer Explorer opens with Ctrl+B
nnoremap <C-B> :BufExplorer<cr>

" When editing a file, always jump to the last known cursor position.
autocmd BufReadPost *
      \ if line("'\"") > 1 && line("'\"") <= line("$") |
      \   exe "normal! g`\"" |
      \ endif


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Autocomplete
"
inoremap <tab> <c-r>=InsertTabWrapper()<cr>
inoremap <s-tab> <c-n>

" Smart tab autocomplete behavior
function! InsertTabWrapper()
  let col = col('.') - 1
  if !col || getline('.')[col - 1] !~ '\k'
    return "\<tab>"
  else
    return "\<c-p>"
  endif
endfunction



"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Vertical alignment configuration (using tabular)
"
" Align = signs
nmap <Leader>a= :Tabularize /=<cr>
vmap <Leader>a= :Tabularize /=<cr>gv
" Align hashrockets
nmap <Leader>a> :Tabularize /=><cr>
vmap <Leader>a> :Tabularize /=><cr>gv
" Align commas
nmap <Leader>a, :Tabularize /,\zs/l1<cr>
vmap <Leader>a, :Tabularize /,\zs/l1<cr>gv
" Align words and fix indentation
nmap <Leader>aw :Tabularize /\s\+\zs/l1<cr>=ip
vmap <Leader>aw :Tabularize /\s\+\zs/l1<cr>gv=gv
" Align comments
nmap <Leader>ac :Tabularize /#<cr>
" Align blocks
nmap <Leader>a{ :Tabularize /{<cr>
vmap <Leader>a{ :Tabularize /{<cr>gv
vmap <Leader>a{ :Tabularize /{<cr>gv

" Aligns tables in cucumber, from tim pope: https://gist.github.com/tpope/287147
function! s:AlignCucumberTables()
  let p = '^\s*|\s.*\s|\s*$'
  if exists(':Tabularize') && getline('.') =~# '^\s*|' && (getline(line('.')-1) =~# p || getline(line('.')+1) =~# p)
    let column = strlen(substitute(getline('.')[0:col('.')],'[^|]','','g'))
    let position = strlen(matchstr(getline('.')[0:col('.')],'.*|\s*\zs.*'))
    Tabularize/|/l1
    normal! 0
    call search(repeat('[^|]*|',column).'\s\{-\}'.repeat('.',position),'ce',line('.'))
  endif
endfunction

" Only enable in cucumber
function! EnableCucumberTables()
  inoremap <silent> <Bar>   <Bar><Esc>:call <SID>AlignCucumberTables()<cr>a
endfunction
au FileType cucumber call EnableCucumberTables()
au FileType markdown call EnableCucumberTables()

function! AlignRubyHashes()
  let p = '^\s*\w\+:\s.*,$'

  " Calculate the range to act. Default Tabularize doesn't do this properly,
  " because the range is determined by a different regex than the alignment
  " regex.
  let from = line('.')
  while (getline(from - 1) =~ p)
    let from -= 1
  endwhile

  let to = line('.')
  while (getline(to + 1) =~ p)
    let to += 1
  endwhile

  " Call Tabularize with the new range
  exe from.','.to.'Tabularize/\(:.*\)\@<!\(:\s\)\zs/l1'
  normal! 0

endfunction

" Align after colons, but only the first one in the line,
" In normal mode, determine range automatically
nmap <Leader>a: :call AlignRubyHashes()<cr>
" In visual mode, the range is already fixed
vmap <Leader>a: :Tabularize/\(:.*\)\@<!:\zs/l1<cr>gv


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Rainbow parenthesis configuration
"
let g:rainbow_active = 1
let g:rainbow_conf = {
  \  'guifgs':  [ '#ea8986', '#a4c38c', '#ffc68d', '#a7cae3', '#e8cefb', '#00a7a0' ],
  \  'ctermfgs': ['lightmagenta', 'lightblue', 'lightyellow', 'lightcyan'],
  \  'operators': '_,_',
  \  'parentheses': ['start=/(/ end=/)/ fold', 'start=/\[/ end=/\]/ fold', 'start=/{/ end=/}/ fold'],
  \  'separately': {
  \    '*': {},
  \    'tex': { 'parentheses': ['start=/(/ end=/)/', 'start=/\[/ end=/\]/'] },
  \    'vim': { 'parentheses': ['start=/(/ end=/)/', 'start=/\[/ end=/\]/', 'start=/{/ end=/}/ fold', 'start=/(/ end=/)/ containedin=vimFuncBody', 'start=/\[/ end=/\]/ containedin=vimFuncBody', 'start=/{/ end=/}/ fold containedin=vimFuncBody'] },
  \    'html': { 'parentheses': ['start=/\v\<((area|base|br|col|embed|hr|img|input|keygen|link|menuitem|meta|param|source|track|wbr)[ >])@!\z([-_:a-zA-Z0-9]+)(\s+[-_:a-zA-Z0-9]+(\=("[^"]*"|'."'".'[^'."'".']*'."'".'|[^ '."'".'"><=`]*))?)*\>/ end=#</\z1># fold'], },
  \    'eruby': { 'parentheses': ['start=/\v\<((area|base|br|col|embed|hr|img|input|keygen|link|menuitem|meta|param|source|track|wbr)[ >])@!\z([-_:a-zA-Z0-9]+)(\s+[-_:a-zA-Z0-9]+(\=("[^"]*"|'."'".'[^'."'".']*'."'".'|[^ '."'".'"><=`]*))?)*\>/ end=#</\z1># fold'], },
  \    'css': 0,
  \  }
  \}


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Linter configuration (using Ale plugin)
"
let g:ale_sign_error   = ' •' " has U+2009 as space
let g:ale_sign_warning = ' •'
let g:ale_sign_column_always = 1

highlight Error   cterm=bold gui=bold guibg=#151515 guifg=DarkRed
highlight Warning cterm=bold gui=bold guibg=#FF0000

" highlight ALEError guibg=#FF0000
" highlight ALEErrorLine guibg=#FF0000
highlight ALEErrorSign ctermfg=9 ctermbg=15 guifg=#C30500
" highlight ALEStyleError guibg=#FF0000

" highlight ALEInfo guibg=#FF0000
" highlight ALEInfoLine guibg=#FF0000

" highlight ALEStyleWarning guibg=#FF0000
highlight ALEWarning gui=undercurl guisp=#7777dd
" highlight ALEWarning guibg=#151566 gui=undercurl
" highlight ALEWarningLine guibg=#FF0000
highlight ALEWarningSign ctermfg=11 ctermbg=15 guifg=#7777dd

"  ale#statusline#Count()
let g:ale_statusline_format = ['⨉ %d', 'w %d', 'OK']
let g:ale_ruby_rubocop_options = '-c ~/.dotfiles/rubocop.yml'
let g:ale_set_loclist = 0
let g:ale_set_quickfix = 0
" let g:ale_lint_on_text_changed = 'normal'
" Only run on save
let g:ale_lint_on_text_changed = 'never'
" if you don't want linters to run on opening a file
" let g:ale_lint_on_enter = 0

" Map prev and back mac shortcuts to traverse buffers
map <D-[> :bprevious<cr>
map <D-]> :bnext<cr>

nmap <silent> <D-k> <Plug>(ale_previous)
nmap <silent> <D-j> <Plug>(ale_next)


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" vim-test configuration
"
let test#strategy = "iterm"
let g:test#preserve_screen = 1
let test#ruby#rspec#executable = 'rspec_focus'
let test#ruby#cucumber#executable = 'cucumber_focus'
let test#elixir#exunit#options = '--stale'
let test#elixir#exunit#options = {
  \ 'suite':   '--stale',
\}
nmap <silent> <leader>t :up<CR>:TestFile<CR>
nmap <silent> <leader>T :up<CR>:TestNearest<CR>
nmap <silent> <leader>l :up<CR>:TestSuite --only-failures<CR>
nmap <silent> <leader>r :up<CR>:TestSuite<CR>
nmap <silent> <leader>g :TestVisit<CR>

" Map ,p to promot variable assignment to let in rspec
map <leader>p :call PromoteToLet()<cr>
function! PromoteToLet()
  :normal! dd
  :exec '?^\s*describe\|fdescribe\|fcontext\|context\|RSpec\.describe\>'
  :normal! p
  :.s/\(\w\+\)\s\+=\s\+\(.*\)$/let(:\1) { \2 }/
  :normal ==
endfunction

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Status line configuration (using lightline)
"
set laststatus=2
set noshowmode
let g:lightline = {
      \ 'colorscheme': 'jellybeans',
      \ 'mode_map': { 'c': 'NORMAL' },
      \ 'active': {
      \   'left': [ [ 'mode', 'paste' ], [ 'filename' ] ],
      \   'right': [ [ 'syntastic', 'lineinfo' ], ['percent'], [ 'fileformat', 'fileencoding', 'filetype' ] ]
      \ },
      \ 'component_function': {
      \   'modified': 'LightlineModified',
      \   'readonly': 'LightlineReadonly',
      \   'fugitive': 'LightlineFugitive',
      \   'filename': 'LightlineFilename',
      \   'fileformat': 'LightlineFileformat',
      \   'filetype': 'LightlineFiletype',
      \   'fileencoding': 'LightlineFileencoding',
      \   'mode': 'LightlineMode',
      \ },
      \ 'component_expand': {
      \   'syntastic': 'ale#statusline#Count',
      \ },
      \ 'component_type': {
      \   'syntastic': 'error',
      \ },
      \ 'separator': { 'left': '', 'right': '' },
      \ 'subseparator': { 'left': '', 'right': '' }
      \ }

function! LightlineModified()
  return &ft =~ 'help\|vimfiler\|gundo' ? '' : &modified ? '+' : &modifiable ? '' : '-'
endfunction

function! LightlineReadonly()
  return &ft !~? 'help\|vimfiler\|gundo' && &readonly ? '' : ''
endfunction

" \ '' != expand('%:t') ? expand('%:t') : '[No Name]') .
function! LightlineFilename()
  return ('' != LightlineReadonly() ? LightlineReadonly() . ' ' : '') .
        \ (&ft == 'vimfiler' ? vimfiler#get_status_string() :
        \  &ft == 'unite' ? unite#get_status_string() :
        \  &ft == 'vimshell' ? vimshell#get_status_string() :
        \ '' != expand('%:t') ? @% : '[No Name]') .
        \ ('' != LightlineModified() ? ' ' . LightlineModified() : '')
endfunction

function! LightlineFugitive()
  if &ft !~? 'vimfiler\|gundo' && exists("*fugitive#head")
    let branch = fugitive#head()
    return branch !=# '' ? ' '.branch : ''
  endif
  return ''
endfunction

function! LightlineFileformat()
  return winwidth(0) > 70 ? &fileformat : ''
endfunction

function! LightlineFiletype()
  return winwidth(0) > 70 ? (&filetype !=# '' ? &filetype : 'no ft') : ''
endfunction

function! LightlineFileencoding()
  return winwidth(0) > 70 ? (&fenc !=# '' ? &fenc : &enc) : ''
endfunction

function! LightlineMode()
  return winwidth(0) > 60 ? lightline#mode() : ''
endfunction


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Ripgrep configuration
"
set wildignore+=*/.git/*,*/tmp/*,*.swp
if executable('rg')
  set grepprg=rg\ --color=never
  let g:ctrlp_user_command = 'rg %s --files --color=never --glob ""'
  let g:ctrlp_use_caching = 0
endif
